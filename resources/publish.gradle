apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri(rootProject.ext.mavenUrl)
            credentials {
                def github = rootProject.file(rootProject.ext.githubProperties)
                def properties = new Properties()
                properties.load(new FileInputStream(github))
                username = properties['github.user']
                password = properties['github.token']
            }
        }
    }
    publications {
        library(MavenPublication) {
            groupId rootProject.ext.libraryGroupId
            artifactId this.name
            version this.version
            artifact("$buildDir/outputs/aar/${this.name}-release.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}